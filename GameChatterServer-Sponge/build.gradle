import org.jooq.util.GenerationTool
import javax.xml.bind.JAXB

group 'ChatCraftServer'
version '1.0-SNAPSHOT'


buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.6.0'
        classpath group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.3.+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile project(':GameChatterServer-Core')
    compile group: 'org.spongepowered', name: 'spongeapi', version: '4.+'
    compile group: 'org.jooq', name: 'jooq', version: '3.7.0'
    runtime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '1.3.3'
}

shadowJar {
    dependencies {
        include(dependency(':GameChatterServer-Core'))
        include(dependency('com.google.code.gson:gson:2.6.2'))
        include(dependency('org.java-websocket:Java-WebSocket:1.3.0'))
    }

    relocate('com.google.gson', 'com.nearce.gamechatter.standalone.internal.gson')
    relocate('org.java-websocket', 'com.nearce.gamechatter.standalone.internal.websocket')

}

build.dependsOn(shadowJar)

task codegen << {
    println 'Generating new schema classes...'
    def writer = new StringWriter()
    def xml = new groovy.xml.MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd') {
        jdbc() {
            driver('org.mariadb.jdbc.Driver')
            url('jdbc:mariadb://localhost:3306')
            user('cc_dev')
            password('cc_dev')
        }
        generator() {
            database() {
                name('org.jooq.util.mariadb.MariaDBDatabase')
                inputSchema('cc_db')
            }
            generate() {
            }
            target() {
                packageName('com.nearce.gamechatter.db.schema')
                directory('src/main/java')
            }
        }
    }

    GenerationTool.main(
            JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
    )
}
